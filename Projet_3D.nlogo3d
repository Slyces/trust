breed [providers provider]
breed [consumers consumer]

consumers-own [
  listProvider
  listConsumer
  ratingData   ;liste des transactions que le consumer a effectuÃ© max 10
  alpha
  utility
  utility_sum
  utility_mean
  nbTransaction
  temp_transaction
  need_update
]
providers-own [
  type_provider
  mu
  sigma
  listCertificate
  max_certificate
  need_update
]

globals [
  service
  utilityIt
  utilityRandom
  dist_table
  log_with_diffusion
  nb_actif
  pW
  pC
  pT
  p_I
  m_PI
  m_PC
  m_PW
  m_PT

  ; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Constantes de l'article â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;
  range_max
  alpha_min
  alpha_max
  H

  ; ---------------------------- Calcul de trust ----------------------------- ;
  branching_factor
  referral_length
  gamma
  lambda

  ; ---------------------------- Nombres d'agents ---------------------------- ;
  good_ratio
  ordinary_ratio
  bad_ratio
  intermitent_ratio

  ; ------------------------- Poids des composantes -------------------------- ;
  W_R

  ; ------------------------- Paliers de performance ------------------------- ;
  pl_perfect
  pl_good
  pl_ok
  pl_bad
  pl_worst
  ; -------------------------- Dynamisme des agents -------------------------- ;
  p_ppc
  p_plp
  deltaphi_p

  p_cpc
  p_plc
  deltaphi_c

  ; ----------------------- Dynamisme des performances ----------------------- ;
  p_profile_switch
  p_mu
  M

]

to setup
  clear-all
  reset-ticks
  ; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ DEFINITION CONSTANTES â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;
  set service "quality"
  ;set range_max round (range_vision);
  set range_max round (range_vision_provider * (1 + coeff_radius_max))
  set H 10
  set alpha_min 0.25
  set alpha_max 1.0
  set p_I []
  set pC []
  set pW []
  set pT []


  ; ------------------------- Constantes pour trust -------------------------- ;
  set branching_factor 2
  set referral_length 5
  set lambda (- 5 / ln 0.5)
  set gamma (- ln 0.5)

  ; ---------------- Intervales de performance selon le type ----------------- ;
  set pl_perfect 10
  set pl_good 5
  set pl_ok 0
  set pl_bad -5
  set pl_worst -10

  ; ------------------------- Poids des composantes -------------------------- ;
  set W_R 2.0
  if Interaction = false [ set W_I 0.0 ]
  if Witness = false [ set W_W 0.0 ]
  if Certificate = false [ set W_C 0.0 ]

  ; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ FIN CONSTANTES â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;

  ; ------------------------ Initialisation consumer ------------------------- ;
  create-consumers nbConsumer [ ; Projection sur la sphÃ¨re
    tilt-up asin (1.0 - random-float 2)
    jump range_circle
  ]
  ask consumers [
    set color blue
    set ratingData []
    set listProvider []
    set listConsumer []
    set need_update false
    ;set alpha 1
    set alpha random_uniform alpha_min alpha_max;
  ]
  ask consumers [ ; CrÃ©e la liste des voisins disponibles;
    ask consumers with [distance_sphere self myself <= range_vision_consumer][
      set listConsumer lput [who] of myself listConsumer
    ]
    set listConsumer remove [who] of self listConsumer
  ]
  ; ------------------------ Initialisation providers ------------------------ ;
  create-providers nbProvider [
    tilt-up asin (1.0 - random-float 2)
    jump range_circle
  ]
  ask providers [
    set type_provider 0
    set listCertificate []
    set max_certificate [0 0 -20 0]
    set need_update false

    ;donne l'id du provider aux consumers
    ask consumers with [distance_sphere self myself <= range_max] [
      set listProvider lput [who] of myself listProvider
    ]
  ]

  ; --------------------------- Types de providers --------------------------- ;
  ; Good
  ask n-of round (nbProvider / 10) providers with [type_provider = 0][
    set color green
    set type_provider "good"
    set mu random_uniform pl_good pl_perfect
    set sigma 1.0
  ]
  ; Odinary
  ask n-of round (nbProvider / 2.5) providers with [type_provider = 0][
    set color orange
    set type_provider "ordinary"
    set mu random_uniform pl_ok pl_good
    set sigma 1.0
  ]
  ; Bad
  ask n-of round (nbProvider / 20) providers[
    set color red
    set type_provider "bad"
    set mu random_uniform pl_worst pl_ok
    set sigma 2.0
  ]
  ; Intermitent
  ask n-of round (nbProvider / 2.22) providers with [type_provider = 0][
    set color violet
    set type_provider "intermittent"
  ]
end

to go
  ; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Interactions â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;
  set log_with_diffusion 0
  set nb_actif 0
  let listRandom [] ; Pour random
  let listNormal [] ;[> Pour IT;

  ; ---------------------- Transaction avec un provider ---------------------- ;
  ask consumers with [length listProvider != 0][
    set listRandom lput give_utility one-of listProvider listRandom
    if random-float 1 < alpha [
      let idProvider 0

      ifelse length listProvider != 1 [ ; Si plusieurs provider son dispo
        set idProvider selectProvider
      ][ set idProvider one-of listProvider ]

      makeTransaction idProvider

      set nb_actif nb_actif + 1
      if distance_sphere self turtle idprovider > range_vision_provider [
        set log_with_diffusion log_with_diffusion + 1
      ]

      set listNormal lput utility listNormal;
    ]
  ]
  set utilityRandom mean listRandom
  set utilityIt mean listNormal;
  ;set utilityIt mean [utility] of consumers with [length listProvider != 0]

  ; ------------------------ Gestion des certificats ------------------------- ;
  if Certificate [
    ask providers with [need_update][
      update_certificate
      set need_update false
      set max_certificate [0 0 -20 0]
    ]
  ]

  ; ----- Ajout de la transaction au consumer [problÃ¨mes de temps rÃ©el] ------ ;
  ask consumers with [need_update][
    set ratingData lput temp_transaction ratingData
    if length ratingData > H [
      set ratingData but-first ratingData
    ]
    set need_update false
  ]
  ifelse empty? p_I [ set m_PI 0 ][set m_PI mean p_I]
  ifelse empty? pW [ set m_PW 0 ][set m_PW mean pW]
  ifelse empty? pC [ set m_PC 0 ][set m_PC mean pC]
  ifelse empty? pT [ set m_PT 0 ][set m_PT mean pT]


  set p_I []
  set pW []
  set pC []
  set pT []

  tick
end

; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Selection d'un provider â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;
to-report selectProvider
  ; let trustValues []
  let selected 0
  let temperature ln (ticks  + 1)
  ;let temperature sqrt (ticks  + 1)

  ; added
  let list_not_trusted []
  let best_provider_id (-1)
  let best_trust_value (-2) ; valeur < min(trust_value)

  foreach listProvider [ ; Pour chaque provider du client
    idProvider ->
    let trust_value Trust idProvider
    ;set trustValues lput (list (Trust idProvider) idProvider) trustValues
    ifelse trust_value != 5 [
      if trust_value > best_trust_value [
        set best_provider_id idProvider
        set best_trust_value trust_value
      ]
    ][
      set list_not_trusted fput idProvider list_not_trusted
    ]
  ]

  if best_trust_value = (-2) [
    report one-of list_not_trusted
  ]
  if empty? list_not_trusted [
      report best_provider_id
  ]

  ; ------------ choix du provider en fonction de la temperature ------------- ;
  let err_profit exp ( best_trust_value * 10 * temperature)
  let err_explore exp ( utility_mean * temperature )

  let p_expl err_explore / ( err_explore + err_profit )
  ifelse random-float 1 < p_expl [
    set selected one-of list_not_trusted
  ][
    set selected best_provider_id
  ]
  report selected
end

; â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Calcul de la confiance â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ ;

; ------------------------ Valeur de confiance finale ------------------------ ;
to-report Trust [idProvider]
  let wt 0
  let w 0

  if W_I > 0 [
    let tr_list_IT getTrListIT self idProvider
    let T_I trustFunction tr_list_IT "IT"
    set p_I lput item 1 T_I p_I
    set w w + (W_I * item 1 T_I)
    set wt wt + (W_I * item 1 T_I * item 0 T_I)
  ]

  if W_W > 0 [
    let tr_list_WR getTrListWR idProvider
    let T_W trustFunction tr_list_WR "WT"
    set pW lput item 1 T_W pW
    set w w + (W_W * item 1 T_W)
    set wt wt + (W_W * item 1 T_W * item 0 T_W)
  ]

  if W_C > 0 [
    let T_C trustFunction [listCertificate] of turtle idProvider "CT"
    set pC lput item 1 T_C pC
    set w w + (W_C * item 1 T_C)
    set wt wt + (W_C * item 1 T_C * item 0 T_C)
  ]

  if W_R > 0 []

  let rho_T w / (W_I + W_W + W_C + W_R)
  set pT lput rho_T pT
  if rho_T = 0 [ report 5 ]

  let T wt / w
  report T
end

; ------------------- Valeurs de confiances intermÃ©diaires ------------------- ;
to-report trustFunction [list_transaction component]
  let up 0
  let down 0
  let weight 0

  ifelse length list_transaction = 0 [ ; T_K
    report list 5 0
  ][
    foreach list_transaction [
      transaction ->
      set weight giveWeight transaction component
      set up (up + weight * item 2 transaction)
      set down (down + weight)
    ]
    let TK up / down

    let PRK (1 - exp (( - gamma) * down)) ; Ïð‘…ð¾

    set up 0
    set down 0
    set weight 0

    foreach list_transaction [ ; Ïð·ð¾
      transaction ->
      set weight giveWeight transaction component
      set down (down + weight)
      set up up + (weight * abs (item 2 transaction - TK))
    ]

    let PDK (1 - (0.5 * (up / down)))
    let PK PRK * PDK

    report list TK PK
  ]
end

; ---------------------- Poids d'un rating (fiabilitÃ©) ----------------------- ;
to-report giveWeight [transaction component]
  ifelse component = "RT" [ ; r = ...
  ][ ; r = (b, c, v, t)
    let time ticks - item 3 transaction
    report exp ( - (time / lambda))
  ]
end

; ------------ Obtention des ratings pertinents (par composante) ------------- ;
; It
to-report getTrListIT [consumer_C id_provider]
  report filter [
    tr -> item 0 tr = id_provider and item 1 tr = service
  ] ratingData
end

; Wr
to-report getTrListWR [id_provider]
  let visited []
  let i 0

  let temp recurcive_transaction_W who id_provider visited i
  report temp
end

to-report recurcive_transaction_W [id_consumer id_provider visited compteur]
  let temp_listTransaction []
  let listTransaction []

  ifelse compteur < referral_length [
    set compteur compteur + 1
    let V best_neighbors id_consumer id_provider

    foreach V [ a ->    ;âˆ€ v âˆˆ V:
      if not member? a visited [  ;si v âˆ‰ visited:
        set visited fput a visited      ;visited.add(v)
        set temp_listTransaction (getTrListIT (turtle a) id_provider)      ;RI_v = R_I(v, b)
        if empty? temp_listTransaction [
          set temp_listTransaction recurcive_transaction_W a id_provider visited compteur
        ]
        set listTransaction sentence listTransaction temp_listTransaction ;merge(R, RI_v)
      ]
    ]
    report listTransaction
  ][ report [] ]
end

; Trie la liste des consumers en fonction de la distance avec le provider
to-report best_neighbors [idConsumer idProvider]
  ifelse empty? [listConsumer] of turtle idConsumer [
    report []
  ][

    let list_distance []
    let retour []
    foreach [listConsumer] of turtle idConsumer [ x ->
      set list_distance lput list x (distance_sphere turtle x turtle idProvider) list_distance
    ]
    set list_distance sort-by [ [a b] ->
      item 1 a < item 1 b
    ]list_distance
    set list_distance take branching_factor list_distance
    foreach list_distance [x ->
      set retour lput item 0 x retour
    ]
    report retour
  ]
end

to makeTransaction [id_provider]
  let temp give_utility id_provider

  ;normalize l'utilitÃ©
  let note utility / 10

  ;fait la moyenne de toutes les transactions dÃ©ja effÃ©ctuÃ©.
  set utility_sum utility + utility_sum
  set nbTransaction nbTransaction + 1
  set utility_mean utility_sum / nbTransaction

  let transaction (list id_provider service note ticks)

  set temp_transaction transaction
  set need_update true

  ;donne la note au provider
  if Certificate [
    ask turtle id_provider [
      if note > item 2 max_certificate[
        set max_certificate transaction
      ]
      set need_update true
    ]
  ]
end

to-report give_utility [id_provider]
  let distance_P 0
  let mean_P 0
  let type_P 0
  let sigma_P 0

  ;distance entre le consumer et le provider
  ask turtle id_provider [
    if range_max > range_vision_provider [ set distance_P distance_sphere self myself ]
    set mean_P mu
    set type_P type_provider
    set sigma_P sigma
  ]

  ifelse type_P = "intermittent" [
    ;random entre -5 et 5
    set utility (random-float 10) - 5
  ][
    set utility random-normal mean_P sigma_P
  ]

  ; ---------- dÃ©gradation linÃ©aire de l'utilitÃ© liÃ©e Ã  la distance ---------- ;
  if range_max > range_vision_provider [
    ;dÃ©gradation linÃ©aire de la distance
    let coeff (- 10 - utility) / (range_max - range_vision_provider)

    if distance_P > range_vision_provider [
      set utility utility + (coeff * (distance_P - range_vision_provider))
    ]
    if distance_P >= range_max [
      set utility -10
    ]
  ]
  if utility < -10 [ set utility -10 ]
  if utility > 10 [ set utility 10 ]
  report utility
end

to update_certificate
  ifelse length listCertificate < H [
    set listCertificate lput max_certificate listCertificate
  ][
    if item 2 max_certificate > item 2 first listCertificate [
      set listCertificate remove first listCertificate listCertificate
      set listCertificate lput max_certificate listCertificate
    ]
  ]
  set listCertificate sort-by [
    [a b] -> item 2 a < item 2 b] listCertificate
end

to-report take [n items]
  report sublist items 0 min list n (length items)
end

to-report random_uniform [min-extreme max-extreme]
  report random-float (max-extreme - min-extreme) + min-extreme
end

;to-report cached_dist [cons_id prov_id];
  ;set cons_id (cons_id - min-one-of consumers);
  ;set prov_id (cons_id - min-one-of providers);
;end;

to-report distance_sphere [a b]
  let temp ((([xcor] of a * [xcor] of b) + ([ycor] of a * [ycor] of b) + ([zcor] of a * [zcor] of b)) / (range_circle * range_circle))
  if temp > 1.0 [
    set temp 1.0
  ]

  report range_circle * acos (temp)
end
@#$#@#$#@
GRAPHICS-WINDOW
0
0
541
542
-1
-1
13.0
1
10
1
1
1
0
1
1
1
-20
20
-20
20
-16
16
1
0
1
ticks
30.0

BUTTON
36
49
103
82
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

BUTTON
36
110
99
143
NIL
go
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SLIDER
18
212
190
245
nbConsumer
nbConsumer
0
500
500.0
1
1
NIL
HORIZONTAL

SLIDER
19
260
191
293
nbProvider
nbProvider
0
100
100.0
1
1
NIL
HORIZONTAL

SLIDER
19
303
227
336
range_vision_provider
range_vision_provider
0
1000
400.0
5
1
NIL
HORIZONTAL

PLOT
226
24
776
256
Utility
ticks
Utility
0.0
100.0
-5.0
10.0
true
false
"" ""
PENS
"default" 1.0 0 -2674135 true "" "plot utilityIt"
"pen-1" 1.0 0 -13345367 true "" "plot utilityRandom"
"pen-2" 1.0 0 -7500403 true "" "plot 0"

SLIDER
19
351
191
384
range_circle
range_circle
1
50
15.0
1
1
NIL
HORIZONTAL

SLIDER
248
348
423
381
coeff_radius_max
coeff_radius_max
0
1
0.4
0.1
1
NIL
HORIZONTAL

SWITCH
810
131
947
164
Witness
Witness
0
1
-1000

SWITCH
809
38
944
71
Interaction
Interaction
0
1
-1000

SWITCH
811
221
948
254
Certificate
Certificate
0
1
-1000

SLIDER
808
75
945
108
W_I
W_I
0
3
2.0
0.1
1
NIL
HORIZONTAL

SLIDER
811
168
948
201
W_W
W_W
0
3
1.0
0.1
1
NIL
HORIZONTAL

SLIDER
811
258
949
291
W_C
W_C
0
3
0.5
0.1
1
NIL
HORIZONTAL

SLIDER
19
400
218
433
range_vision_consumer
range_vision_consumer
0
1000
400.0
5
1
NIL
HORIZONTAL

@#$#@#$#@
## WHAT IS IT?

(a general understanding of what the model is trying to show or explain)

## HOW IT WORKS

(what rules the agents use to create the overall behavior of the model)

## HOW TO USE IT

(how to use the model, including a description of each of the items in the Interface tab)

## THINGS TO NOTICE

(suggested things for the user to notice while running the model)

## THINGS TO TRY

(suggested things for the user to try to do (move sliders, switches, etc.) with the model)

## EXTENDING THE MODEL

(suggested things to add or change in the Code tab to make the model more complicated, detailed, accurate, etc.)

## NETLOGO FEATURES

(interesting or unusual features of NetLogo that the model uses, particularly in the Code tab; or where workarounds were needed for missing features)

## RELATED MODELS

(models in the NetLogo Models Library and elsewhere which are of related interest)

## CREDITS AND REFERENCES

(a reference to the model's URL on the web if it has one, as well as any other necessary credits, citations, and links)
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 3D 6.0.2
@#$#@#$#@
need-to-manually-make-preview-for-this-model
@#$#@#$#@
@#$#@#$#@
<experiments>
  <experiment name="experiment" repetitions="10" runMetricsEveryStep="true">
    <setup>setup</setup>
    <go>go</go>
    <timeLimit steps="500"/>
    <metric>mean p_I</metric>
    <metric>mean pW</metric>
    <metric>mean pC</metric>
    <metric>mean pT</metric>
    <enumeratedValueSet variable="Certificate">
      <value value="true"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="range_circle">
      <value value="15"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="range_vision_provider">
      <value value="400"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="Interaction">
      <value value="true"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="W_C">
      <value value="0.5"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="nbConsumer">
      <value value="500"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="nbProvider">
      <value value="100"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="W_W">
      <value value="1"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="coeff_radius_max">
      <value value="0.4"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="range_vision_consumer">
      <value value="400"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="W_I">
      <value value="2"/>
    </enumeratedValueSet>
    <enumeratedValueSet variable="Witness">
      <value value="true"/>
    </enumeratedValueSet>
  </experiment>
</experiments>
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
